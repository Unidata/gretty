buildscript {
  project.ext {
    if(has('privateRepoDir')) {
      privateRepoDir = privateRepoDir instanceof File ? privateRepoDir.path : privateRepoDir.toString()
      if(!new File(privateRepoDir).isAbsolute())
        privateRepoDir = new File(projectDir, privateRepoDir).canonicalPath
    } else
      privateRepoDir = new File(project.projectDir, '../build/privateRepo').canonicalPath
  }
  
  def topGradleProps = new Properties()
  new File(projectDir, '../gradle.properties').withInputStream {
    topGradleProps.load(it)
  }
  ext.topGradleProps = topGradleProps.asImmutable()
  
  topGradleProps.each { key, value ->
    if(key != 'group' && key != 'version' && key != 'rootProjectName')
      project.ext[key] = value
  }

  repositories {
    maven {
      url "file:$privateRepoDir"
    }
    jcenter()
  }

  dependencies {
    classpath "${topGradleProps.group}:gretty:${topGradleProps.version}"
    classpath "${group}:gretty-integrationTest:${topGradleProps.version}"
  }
}

allprojects {
  version = topGradleProps.version
}
